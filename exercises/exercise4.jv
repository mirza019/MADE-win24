pipeline TemperaturePipeline {

    // processing starts here
    DownloadZip 
        -> UnzipFile
        -> PickCSV 
        -> ReadRawData
        -> ParseCSV
        -> RenameHeaders
        -> RenameBattery
        -> ValidateData
        -> ConvertTemp
        -> ConvertBatteryTemp
        -> SaveData;

    // get the zip file
    block DownloadZip oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    // unzip the file
    block UnzipFile oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    // locate the csv file
    block PickCSV oftype FilePicker {
        path: "/data.csv";
    }

    // read csv as plain text
    block ReadRawData oftype TextFileInterpreter {}

    // parse the csv data
    block ParseCSV oftype CSVInterpreter {
        delimiter: ";";
    }

    // rename main columns from german to english
    block RenameHeaders oftype CellWriter {
        at: range A1:E1; 
        write: ["id", "producer", "model", "month", "temperature"];
    }

    // rename battery temperature column
    block RenameBattery oftype CellWriter {
        at: cell J1; 
        write: ["battery_temperature"];
    }

    // validate the data columns
    block ValidateData oftype TableInterpreter {
        header: true;
        columns: [
            "id" oftype integer,
            "producer" oftype text,
            "model" oftype text,
            "month" oftype integer,
            "temperature" oftype decimal,
            "battery_temperature" oftype decimal
        ];
    }

    // convert temperature to fahrenheit
    transform CelsiusToFahrenheit {
        from TempCelsius oftype decimal;
        to TempFahrenheit oftype decimal;

        TempFahrenheit: 32 + ((TempCelsius / 5) * 9);
    }

    // apply temperature conversion
    block ConvertTemp oftype TableTransformer {
        inputColumns: ["temperature"];
        outputColumn: "temperature";
        uses: CelsiusToFahrenheit;
    }

    // apply battery temperature conversion
    block ConvertBatteryTemp oftype TableTransformer {
        inputColumns: ["battery_temperature"];
        outputColumn: "battery_temperature";
        uses: CelsiusToFahrenheit;
    }

    // save the processed data
    block SaveData oftype SQLiteLoader {
        table: "temperatures";
        file: "./temperatures.sqlite";
    }
}
